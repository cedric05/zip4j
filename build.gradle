plugins {
    id 'java-library'
    id 'maven-publish'
    id "com.github.breadmoirai.github-release" version "2.2.12"
}
sourceCompatibility = 1.8
targetCompatibility = 1.8
repositories {
    jcenter()
}

dependencies {
    testImplementation 'junit:junit:4.12'
}

group 'com.codebuds'
archivesBaseName = 'zip4j'

def mainVersion = '0.1'
def buildNumber = '7'

version = "${mainVersion}.${buildNumber}"
def oauthtoken = project.findProperty("gpr.key") ?: System.getenv("TOKEN") ?: ""
publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/cedric05/zip4j")
            credentials {
                username = project.findProperty("gpr.user")
                password = oauthtoken
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

githubRelease {
    token oauthtoken
    owner "cedric05"
    repo "zip4j"
    targetCommitish "master" // by default this is set to "master"
    releaseAssets fileTree("$project.buildDir.absolutePath/libs")
    overwrite true
    dryRun false
}


test {
    reports {
        junitXml.enabled = true
        html.enabled = false
    }
}

task srcZip(type: Zip) {
    classifier = 'src'
    from sourceSets*.allSource
    destinationDirectory = file("$project.buildDir.absolutePath/libs")
}

task increment {
    group 'Build Setup'
    description 'Increases the Buildnumber'
    doLast {
        def bn = buildFile.getText().find(buildNumber) //get this build file's text and extract the buildNumber
        int inc = bn.toInteger() + 1
        String s = buildFile.getText().replaceFirst("def buildNumber = '" + bn + "'", "def buildNumber = '" + inc + "'")
        buildFile.setText(s) //replace the build file's text
    }
}
